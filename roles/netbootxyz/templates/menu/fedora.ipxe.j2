#!ipxe

# Fedora Operating System
# https://getfedora.org/

isset ${dhcp-server} && set ipparam ip=dhcp || set ipparam ip=${ip}::${gateway}:${netmask}:::none nameserver=${dns}
set ipparam BOOTIF=${netX/mac} ${ipparam}

goto ${menu} ||

:fedora
clear osversion
clear sku_type
clear ova
set rh_arch ${arch}
iseq ${arch} arm64 && set rh_arch aarch64
set os {{ releases.fedora.name }}
menu ${os} - ${rh_arch}
item --gap Latest Releases
{% for item in releases.fedora.versions %}
item {{ item.code_name }} ${space} ${os} {{ item.name }}
{% endfor %}
iseq ${rh_arch} x86_64 && item rawhide ${space} ${os} rawhide ||
isset ${osversion} || choose osversion || goto linux_menu
set ova ${os} ${osversion}
goto product_sku

:product_sku
menu ${os} ${rh_arch} sku type
item Everything ${ova} Everything
item Server ${ova} Server
iseq ${rh_arch} x86_64 && item Silverblue ${ova} Silverblue ||
isset ${sku_type} || choose sku_type || goto fedora
set dir ${fedora_base_dir}/releases/${osversion}/${sku_type}/${rh_arch}/os
iseq ${osversion} rawhide && set dir ${fedora_base_dir}/development/${osversion}/${sku_type}/${rh_arch}/os ||
set ova ${ova} ${sku_type}
echo ${cls}
goto boottype

:boottype
menu ${os} ${rh_arch} boot type
item normal ${ova} graphical install
item text ${ova} text install
item rescue ${ova} rescue
item kickstart ${ova} specify kickstart url
isset ${bt} || choose bt || goto fedora
echo ${cls}
iseq ${bt} normal && goto boot ||
goto ${bt}

:text
set params inst.text ||
goto boot

:rescue
set params rescue ||
goto boot

:kickstart
echo -n Specify kickstart URL for ${os} ${osversion}: && read ksurl
set params ks=${ksurl} ||
goto boot

:boot
imgfree
kernel ${fedora_mirror}/${dir}/images/pxeboot/vmlinuz inst.repo=${fedora_mirror}/${dir} ${params} ${ipparam} {{ kernel_params }}
initrd ${fedora_mirror}/${dir}/images/pxeboot/initrd.img
echo
echo MD5sums:
md5sum vmlinuz initrd.img
boot
goto linux_menu

:linux_menu
clear menu
exit 0
