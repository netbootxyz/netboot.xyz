#!ipxe

goto ${menu} ||

:linux_menu
menu Linux Installers - Current Arch [ ${arch} ]
item --gap Popular Linux Operating Systems:
item almalinux ${space} AlmaLinux
item alpinelinux ${space} Alpine Linux
item centos ${space} CentOS
item debian ${space} Debian
item fedora ${space} Fedora
item coreos ${space} Fedora CoreOS
item gentoo ${space} Gentoo
item nixos ${space} NixOS
item opensuse ${space} openSUSE
item oracle ${space} Oracle Linux
item rhel ${space} Red Hat Enterprise Linux
item rockylinux ${space} Rocky Linux
item talos ${space} Talos
item ubuntu ${space} Ubuntu
item photon ${space} VMware Photon
choose menu || goto linux_exit
echo ${cls}
goto ${menu} ||
iseq ${sigs_enabled} true && goto verify_sigs || goto change_menu

:verify_sigs
imgverify ${menu}.ipxe ${sigs}${menu}.ipxe.sig || goto error
goto change_menu

:change_menu
chain ${menu}.ipxe || goto error
goto linux_menu

:linux_exit
clear menu
exit 0
