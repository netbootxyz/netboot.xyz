#!ipxe

goto ${menu} ||

:linux_menu
menu Linux Installers - Current Arch [ ${arch} ]
iseq ${arch} x86_64 && set arch_a amd64 || set arch_a ${arch}
item --gap Popular Linux Operating Systems:
item archlinux ${space} Arch Linux
item centos ${space} CentOS 
item debian ${space} Debian
item fedora ${space} Fedora
item mageia ${space} Mageia
item manjaro ${space} Manjaro Linux
item opensuse ${space} openSUSE
item ubuntu ${space} Ubuntu
item --gap All Others:
item alpinelinux ${space} Alpine Linux
item antergos ${space} Antergos
item coreos ${space} CoreOS
item devuan ${space} Devuan
item gentoo ${space} Gentoo Linux
item ipfire ${space} IPFire
item rancheros ${space} RancherOS
item rhel ${space} Red Hat Enterprise Linux
item scientific ${space} Scientific
item slackware ${space} Slackware
item tinycore ${space} Tiny Core Linux
choose menu || goto linux_exit
echo ${cls}
goto ${menu} ||
iseq ${sigs_enabled} true && goto verify_sigs || goto change_menu

:verify_sigs
imgverify ${menu}.ipxe ${sigs}${menu}.ipxe.sig || goto error
goto change_menu

:change_menu
chain ${menu}.ipxe || goto error
goto linux_menu

:linux_exit
clear menu
exit 0
