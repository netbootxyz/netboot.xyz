#!ipxe

goto ${menu} ||

:linux_menu
menu Linux Operating Systems - Current Selected Architecture [ ${arch} ]
iseq ${arch} x86_64 && set arch_a amd64 || set arch_a ${arch}
item --gap Operating Systems:
item archlinux ${space} ArchLinux
item centos ${space} CentOS 
item debian ${space} Debian
item fedora ${space} Fedora
item gentoo ${space} Gentoo
item opensuse ${space} openSUSE
item scientific ${space} Scientific
item ubuntu ${space} Ubuntu
item tinycore ${space} TinyCoreLinux
# Options
item --gap Options:
iseq ${arch} x86_64 && set bits 64 || set bits 32
item changebits ${space} Architecture: ${arch} (${bits}bit)
choose menu || goto linux_exit
echo ${cls}
goto ${menu} ||
chain ${menu}.ipxe || goto error
goto linux_exit

:linux_exit
clear menu
chain menu.ipxe

:changebits
iseq ${arch} x86_64 && set arch i386 || set arch x86_64
goto linux_menu

:fedora
chain fedora.ipxe
goto linux_menu

:centos
chain centos.ipxe
goto linux_menu

:debian
chain debian.ipxe
goto linux_menu

:ubuntu
chain ubuntu.ipxe
goto linux_menu

:opensuse
chain opensuse.ipxe
goto linux_menu

:scientific
chain scientific.ipxe
goto linux_menu
