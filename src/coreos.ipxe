#!ipxe

# Container Linux by CoreOS
# https://www.coreos.com
# For further info on:
# iPXE and CoreOS Container Linux: https://coreos.com/os/docs/latest/booting-with-ipxe.html
# Setting up Cloud Config: https://github.com/coreos/coreos-cloudinit
# 64-bit only
 
goto ${menu}

:coreos
set os Container Linux
menu ${os}
item --gap ${os}:
item stable ${space} ${os} Stable Channel
item beta ${space} ${os} Beta Channel
item alpha ${space} ${os} Alpha Channel
item cloud_config ${space} Set cloud-config-url: ${cloud-config-url}
choose --default ${menu} menu || goto coreos_exit
echo ${cls}
goto ${menu} ||
goto coreos_exit

:stable
:beta
:alpha
set release ${menu}
set base-url http://${release}.release.core-os.net/amd64-usr/current
kernel ${base-url}/coreos_production_pxe.vmlinuz ${coreos_params} ${console} coreos.autologin=tty1 coreos.autologin=ttyS0 initrd=coreos_production_pxe_image.cpio.gz
initrd ${base-url}/coreos_production_pxe_image.cpio.gz
boot
goto coreos_exit

:cloud_config
echo -n Please set Cloud Config URL: && read cloud-config-url
set coreos_params cloud-config-url=${cloud-config-url}
clear menu
goto coreos

:coreos_exit
clear menu
exit 0
