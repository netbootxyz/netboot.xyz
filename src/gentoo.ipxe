#!ipxe

# Gentoo Operating System
# http://www.gentoo.org

# Note: in order to boot Gentoo via iPXE, the method at http://blog.dastrup.com/?p=12 has been utilized to 
# to allow for the Gentoo minimal disk to boot.  I've regenerated the latest boot disks and have put
# them on ${boot_domain} so that you can boot into the live cd

goto ${menu}

:gentoo
set os gentoo
menu Gentoo ${arch_a}
item 20140116 Gentoo minimal CD version - 20140116
choose version || goto gentoo_exit
goto ${version}

:20140116
set gentoopath http://${boot_domain}/distros/gentoo/amd64/20140116
echo Please note the gentoo ISO has been regenerated to work with iPXE
echo using the method here: http://blog.dastrup.com/?p=12
echo You will need to set up networking manually.
kernel ${gentoopath}/gentoo initrd=gentoo.igz root=/dev/ram0 init=/linuxrc loop=/image.squashfs looptype=squashfs cdroot=1 real_root=/ dokeymap vga=791 nodhcp
initrd ${gentoopath}/gentoo.igz
boot
goto gentoo

:gentoo_exit
clear menu
chain linux.ipxe
