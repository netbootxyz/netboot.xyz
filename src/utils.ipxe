#!ipxe

menu Utilities - Image Sig Checks: [${img_sigs_enabled}]
item --gap Utilities:
item alt_linux_rescue ${space} ALT Linux Rescue
item avg ${space} AVG Rescue CD
item breakin ${space} Breakin
item clonezilla ${space} Clonezilla
item dban ${space} DBAN
item gparted ${space} GParted
item memtest-501 ${space} Memtest86+ 5.01
item partition-wizard ${space} Partition Wizard
item pogostick ${space} Pogostick - Offline Windows Password and Registry Editor
item supergrub ${space} Super Grub2 Disk
item sysrcd ${space} SystemRescueCD
item ubcd ${space} Ultimate Boot CD (UBCD)
item --gap netboot.xyz tools:
item nbxyz-custom ${space} Set Github User [user: ${github_user}]
item testdistro ${space} Test Distribution ISO
item testpr ${space} Test netboot.xyz branch
choose --default ${menu} menu || goto utils_exit
echo ${cls}
goto ${menu} ||
chain ${menu}.ipxe || goto utils_exit
goto utils_exit

:alt_linux_rescue
iseq ${arch} x86_64 && goto alt_linux_rescue_x64 ||
set util_path nightly.altlinux.org/p8/release/alt-p8-rescue-20170312-i586.iso
set util_file alt-p8-rescue-20170312-i586.iso
goto boot_memdisk

:alt_linux_rescue_x64
set util_path nightly.altlinux.org/p8/release/alt-p8-rescue-20170312-x86_64.iso
set util_file alt-p8-rescue-20170312-x86_64.iso
goto boot_memdisk

:avg
set avg_version 160420a12074
set util_path download.avg.com/filedir/inst/avg_arl_cdi_all_120_${avg_version}.iso
set util_file avg_arl_cdi_all_120_${avg_version}.iso
goto boot_memdisk

:breakin
set breakin_version 4.26.1-53
set util_path www.advancedclustering.com/wp-content/uploads/2017/02/bootimage-${breakin_version}.iso
set util_file bootimage-${breakin_version}.iso
goto boot_memdisk

:clonezilla
set clonezilla_version 2.5.2-31
set util_path sourceforge.net/projects/clonezilla/files/clonezilla_live_stable/${clonezilla_version}/clonezilla-live-${clonezilla_version}-amd64.iso/download
set util_file clonezilla-live-${clonezilla_version}-amd64.iso
goto boot_memdisk

:dban
set dban_version 2.3.0
set util_path sourceforge.net/projects/dban/files/dban/dban-${dban_version}/dban-${dban_version}_i586.iso/download
set util_file dban-${dban_version}_i586.iso
goto boot_memdisk

:gparted
set gparted_version 0.30.0-1
set util_path sourceforge.net/projects/gparted/files/gparted-live-stable/${gparted_version}/gparted-live-${gparted_version}-amd64.iso/download
set util_file gparted-live-${gparted_version}-amd64.iso
goto boot_memdisk

:memtest-501
set util_path boot.netboot.xyz/utils/memtest86-5.01.0
set util_file memtest86-5.01.0
goto boot_memtest

:partition-wizard
set util_path www.partitionwizard.com/download/pwfree91-x64.iso
set util_file pwfree91-x64.iso
goto boot_memdisk

:pogostick
set util_path pogostick.net/~pnh/ntpasswd/cd140201.zip
set util_file cd140201.zip
goto boot_memdisk

:supergrub
set util_path sourceforge.net/projects/supergrub2/files/2.02s4/super_grub2_disk_hybrid_2.02s4.iso/download
set util_file super_grub2_disk_hybrid_2.02s4.iso
goto boot_memdisk

:sysrcd
set sysrcdpath sourceforge.net/projects/netboot-xyz/files/distros/sysresccd-x86/4.8.1
kernel --name altker64 https://${sysrcdpath}/altker64/download setkmap=no net.ifnames=0 backstore=off
initrd --name initram.igz https://${sysrcdpath}/initram.igz/download
initrd --name sysrcd.dat https://${sysrcdpath}/sysrcd.dat/download /sysrcd.dat
echo
echo MD5sums:
md5sum altker64 initram.igz sysrcd.dat
iseq ${img_sigs_enabled} true && goto verify_sigs_sysrcd || goto skip_sigs_sysrcd
:verify_sigs_sysrcd
echo
echo Checking signatures...
imgverify altker64 ${sigs}${menu}/altker64.sig || goto error
imgverify initram.igz ${sigs}${menu}/initram.igz.sig || goto error
imgverify sysrcd.dat ${sigs}${menu}/sysrcd.dat.sig || goto error
echo Signatures verified!
echo
:skip_sigs_sysrcd
boot
goto utils_exit

:ubcd
set util_path mirror.sysadminguide.net/ubcd/ubcd537.iso
set util_file ubcd537.iso
goto boot_memdisk

:boot_memdisk
imgfree
kernel ${memdisk} iso raw
initrd --name ${util_file} http://${util_path}
echo
echo MD5sums:
md5sum memdisk ${util_file}
iseq ${img_sigs_enabled} true && goto verify_sigs || goto skip_sigs
:verify_sigs
echo
echo Checking signatures...
imgverify memdisk ${sigs}memdisk.sig || goto error
imgverify ${util_file} ${sigs}${menu}/${util_file}.sig || goto error
echo Signatures verified!
echo
:skip_sigs
boot
goto utils_exit

:boot_memtest
chain https://${util_path} && goto main_menu ||
goto utils_exit

:nbxyz-custom
echo EXPERIMENTAL
echo 
echo Make sure you have a fork of https://github.com/antonym/netboot.xyz-custom.
echo You can then customize your fork as needed and set up your own custom options.
echo Once your username is set, a custom option will appear on the main menu.
echo
echo -n Please enter your Github username: ${} && read github_user
goto utils_exit 

:testdistro
echo This option will allow you to test booting an ISO using memdisk. Please
echo specify the url of the ISO you want to test and it will automatically
echo attempt to load the ISO using memdisk.
echo -n URL: ${} && read distro_iso
kernel ${memdisk} iso raw
initrd ${distro_iso}
boot
goto utils_exit

:testpr
clear github_user
clear github_hash
echo This will chainload into a testing branch of netboot.xyz.  You'll need to enter
echo your Github User and the first part of the commit hash of the commit you want
echo to test or the branch name.  This assumes you are testing from a netboot.xyz 
echo repo.
echo
echo -n Specify Github User Name: ${} && read github_user
echo -n Specify branch name or hash ( i.e. my_feature or 30b7ca ): ${} && read github_hash
echo
echo Attempting to chainload:
chain --autofree https://raw.githubusercontent.com/${github_user}/netboot.xyz/${github_hash}/src/menu.ipxe || echo Unable to find branch... && sleep 5 && goto utils_exit
goto utils_exit

:utils_exit
clear menu
exit 0
