#!ipxe

# FreeBSD Operating System
# http://www.freebsd.org

:freebsd_menu
menu Please pick a FreeBSD version
item 11.1 FreeBSD 11.1
item 10.2 FreeBSD 10.2
item 10.1 FreeBSD 10.1
item 10.0 FreeBSD 10.0
choose ver || goto freebsd_exit

iseq ${ver} 11.1 && set image_ver_major 11 ||

iseq ${ver} 11.1 && set image_ver 11.1-RELEASE ||
iseq ${ver} 10.2 && set image_ver 10.2-RELEASE ||
iseq ${ver} 10.1 && set image_ver 10.1-RELEASE ||
iseq ${ver} 10.0 && set image_ver 10.0-RELEASE ||

iseq ${arch} x86_64 && goto freebsd_x64 ||
set freebsd_arch i386
goto boot_freebsd

:freebsd_x64
set freebsd_arch amd64
goto boot_freebsd

:boot_freebsd
# set src http://boot.rackspace.com/distros/freebsd/${ver}/FreeBSD-${image_ver}-${freebsd_arch}-bootonly-mfsbsd.hd
set src http://mfsbsd.vx.sk/files/images/${image_ver_major}/mfsbsd-${image_ver}-${freebsd_arch}.img
imgfree
echo This loads an mfsbsd installer (http://mfsbsd.vx.sk/).  
echo Root password for all images: mfsroot
echo You'll need to configure networking manually for the installer disk as 
echo it uses dhcp by default:
echo ifconfig xn0 inet <public ip> netmask 255.255.255.0
echo route delete default; route add default <public ip x.x.x.1>
echo echo "nameserver x.x.x.x" > /etc/resolv.conf
echo Once network is configured, you can launch the usual installer:
echo bsdinstall
initrd ${src}
chain ${memdisk} harddisk raw
exit

:freebsd_exit
clear menu
exit 0
