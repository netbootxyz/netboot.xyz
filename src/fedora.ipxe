#!ipxe

# Fedora Operating System
# https://getfedora.org/

goto ${menu} ||

:fedora
clear osversion
clear sku_type
clear ova
set os Fedora
menu Fedora - ${arch} - Image Sig Checks: [${img_sigs_enabled}]
item --gap Latest Releases
item 27 ${space} ${os} 27
item 26 ${space} ${os} 26
isset ${osversion} || choose osversion || goto linux_menu
set ova ${os} ${osversion}
goto product_sku

:product_sku
menu ${os} ${arch} sku type
item Everything ${ova} Everything
item Server ${ova} Server
item Workstation ${ova} Workstation
isset ${sku_type} || choose sku_type || goto fedora
set dir ${fedora_base_dir}/releases/${osversion}/${sku_type}/${arch}/os
set ova ${ova} ${sku_type}
echo ${cls}
goto boottype

:boottype
menu ${os} ${arch} boot type
item normal ${ova} install
item rescue ${ova} rescue
item kickstart ${ova} specify kickstart url
isset ${bt} || choose bt || goto fedora
echo ${cls}
iseq ${bt} rescue && goto rescue ||
iseq ${bt} kickstart && goto kickstart ||
goto boot

:rescue
set params rescue ||
goto boot

:kickstart
echo -n Specify kickstart URL for ${os} ${osversion}: && read ksurl
set params ks=${ksurl} ||
goto boot

:boot
imgfree
kernel http://${fedora_mirror}/${dir}/images/pxeboot/vmlinuz repo=http://${fedora_mirror}/${dir} ${params} ${console} initrd=initrd.img
initrd http://${fedora_mirror}/${dir}/images/pxeboot/initrd.img
echo
echo MD5sums:
md5sum vmlinuz initrd.img
iseq ${img_sigs_enabled} true && goto verify_sigs || goto skip_sigs
:verify_sigs
echo
echo Checking signatures...
imgverify vmlinuz ${sigs}${dir}/images/pxeboot/vmlinuz.sig || goto error
imgverify initrd.img ${sigs}${dir}/images/pxeboot/initrd.img.sig || goto error
echo Signatures verified!
echo
:skip_sigs
boot
goto linux_menu

:linux_menu
clear menu
exit 0
