#!ipxe

# Fedora Operating System
# https://getfedora.org/

goto ${menu} ||

:fedora
clear osversion
clear sku_type
clear ova
set os Fedora
menu Fedora - ${arch} - Image Sig Checks: [${img_sigs_enabled}]
item --gap Latest Releases
item 28 ${space} ${os} 28
item 27 ${space} ${os} 27
isset ${osversion} || choose osversion || goto linux_menu
set ova ${os} ${osversion}
goto product_sku

:product_sku
menu ${os} ${arch} sku type
item Everything ${ova} Everything
item Server ${ova} Server
item Workstation ${ova} Workstation
item Atomic ${ova} Atomic
isset ${sku_type} || choose sku_type || goto fedora
set dir ${fedora_base_dir}/releases/${osversion}/${sku_type}/${arch}/os
iseq ${sku_type} Atomic && iseq ${osversion} 28 && set dir fedora-alt/atomic/stable/Fedora-Atomic-28-20180515.1/AtomicHost/x86_64/os ||
iseq ${sku_type} Atomic && iseq ${osversion} 27 && set dir fedora-alt/atomic/stable/Fedora-Atomic-27-20180419.0/Atomic/x86_64/os ||
set ova ${ova} ${sku_type}
echo ${cls}
goto boottype

:boottype
menu ${os} ${arch} boot type
item normal ${ova} install
item rescue ${ova} rescue
item kickstart ${ova} specify kickstart url
isset ${bt} || choose bt || goto fedora
echo ${cls}
iseq ${bt} rescue && goto rescue ||
iseq ${bt} kickstart && goto kickstart ||
goto boot

:rescue
set params rescue ||
goto boot

:kickstart
echo -n Specify kickstart URL for ${os} ${osversion}: && read ksurl
set params ks=${ksurl} ||
goto boot

:boot
imgfree
kernel http://${fedora_mirror}/${dir}/images/pxeboot/vmlinuz repo=http://${fedora_mirror}/${dir} ${params} ${console} BOOTIF=${netX/mac} ip=dhcp initrd=initrd.img
initrd http://${fedora_mirror}/${dir}/images/pxeboot/initrd.img
echo
echo MD5sums:
md5sum vmlinuz initrd.img
iseq ${img_sigs_enabled} true && goto verify_sigs || goto skip_sigs
:verify_sigs
echo
echo Checking signatures...
imgverify vmlinuz ${sigs}${dir}/images/pxeboot/vmlinuz.sig || goto error
imgverify initrd.img ${sigs}${dir}/images/pxeboot/initrd.img.sig || goto error
echo Signatures verified!
echo
:skip_sigs
boot
goto linux_menu

:linux_menu
clear menu
exit 0
