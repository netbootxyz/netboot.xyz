#!ipxe

# CentOS Operating System
# http://www.centos.org

goto ${menu} ||

:centos
clear osversion
set os CentOS
menu ${os} - ${arch} - Image Sig Checks: [${img_sigs_enabled}]
item 7.5.1804 ${os} 7.5
item 6.10 ${os} 6.10
isset ${osversion} || choose osversion || goto linux_menu
echo ${cls}
iseq ${osversion} 6.10 && set dir ${menu}/${osversion}/os/${arch} ||
set dir ${centos_base_dir}/${osversion}/os/x86_64
set repo http://${centos_mirror}/${dir}
goto boottype

:boottype
set ova ${os} ${osversion}
menu ${os} ${arch} boot type
item graphical ${ova} graphical installer
item text ${ova} text based installer
item rescue ${ova} rescue
item kickstart ${ova} set kickstart url [ ${ksurl} ]
item kickstart_device ${ova} set ksdevice [ ${ksdevice} ]
isset ${bt} || choose bt || goto centos
echo ${cls}
iseq ${bt} text && goto text ||
iseq ${bt} rescue && goto rescue ||
iseq ${bt} kickstart && goto kickstart ||
iseq ${bt} kickstart_device && goto kickstart_device ||
goto bootos_images

:text
set params text ||
goto bootos_images

:rescue
set params rescue ||
goto bootos_images

:kickstart
echo -n Specify kickstart URL for ${os} ${osversion}: && read ksurl
set params ks=${ksurl} ||
clear bt
goto boottype

:kickstart_device
echo -n Specify ksdevice param for ${os} ${osversion}: && read ksdevice
set ksdevice ${ksdevice} ||
clear bt
goto boottype

:bootos_images
imgfree
kernel http://${centos_mirror}/${dir}/images/pxeboot/vmlinuz repo=${repo} ${params} ${console} BOOTIF=${netX/mac} ip=dhcp initrd=initrd.img
initrd http://${centos_mirror}/${dir}/images/pxeboot/initrd.img
echo
echo MD5sums:
md5sum vmlinuz initrd.img
iseq ${img_sigs_enabled} true && goto verify_sigs || goto skip_sigs
:verify_sigs
echo
echo Checking signatures...
imgverify vmlinuz ${sigs}${dir}/images/pxeboot/vmlinuz.sig || goto error
imgverify initrd.img ${sigs}${dir}/images/pxeboot/initrd.img.sig || goto error
echo Signatures verified!
echo
:skip_sigs
boot
goto linux_menu

:linux_menu
clear menu
exit 0
