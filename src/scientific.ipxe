#!ipxe

# Scientific Linux Operating System
# https://www.scientificlinux.org/

goto ${menu} ||

:scientific
clear osversion
set os Scientific Linux
menu ${os} ${arch}
item 7.5 ${os} 7.5
item 7.4 ${os} 7.4
item 6.10 ${os} 6.10
item 6.9 ${os} 6.9
item other Choose other version [o]
isset ${osversion} || choose osversion || goto linux_menu
iseq ${osversion} other || goto scientific_skip_read_osversion
echo ${cls}
echo -n Enter version: ${} && read osversion
:scientific_skip_read_osversion
set dir linux/scientific/${osversion}/${arch}/os
set repo http://ftp1.scientificlinux.org/${dir}

goto boottype

:boottype
set ova ${os} ${osversion}
menu ${os} ${arch} boot type
item normal ${ova}
item rescue ${ova} rescue
item kickstart ${ova} specify kickstart url
isset ${bt} || choose bt || goto scientific
echo ${cls}
iseq ${bt} rescue && goto rescue ||
iseq ${bt} kickstart && goto kickstart ||
goto bootos_images

:rescue
set params rescue ||
goto bootos_images

:kickstart
echo -n Specify kickstart URL for ${os} ${osversion}: && read ksurl
set params ks=${ksurl} ||
goto bootos_images

:bootos_images
imgfree
kernel http://ftp1.scientificlinux.org/${dir}/images/pxeboot/vmlinuz repo=${repo} ${params} ${netcfg} ${console}
initrd http://ftp1.scientificlinux.org/${dir}/images/pxeboot/initrd.img
boot
goto linux_menu

:linux_menu
clear menu
exit 0
