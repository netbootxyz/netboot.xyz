#!ipxe

# Debian Operating System
# http://www.debian.org

goto ${menu}

:debian
set os debian
iseq ${manufacturer} Xen && set netcfg netcfg/get_ipaddress=${netX/ip} netcfg/get_netmask=${netX/netmask} netcfg/get_gateway=${netX/gateway} netcfg/get_nameservers=${dns} netcfg/confirm_static=true netcfg/choose_interface=eth0 netcfg/disable_autoconfig=true ||

menu Debian ${arch_a}
item jessie Debian 8.0 (jessie)
item wheezy Debian 7.0 (wheezy)
item squeeze Debian 6.0 (squeeze)
item stretch Debian stretch (testing)
item sid Debian sid (unstable) 
choose version || goto debian_exit
set mirrorcfg mirror/suite=${version} mirror/country=manual mirror/http/hostname=${mirror} mirror/http/directory=/${os}

:deb_boot_type
set dir ${os}/dists/${version}/main/installer-${arch_a}/current/images/netboot/

menu ${os} boot parameters
item text ${os} text based install
item graphical ${os} graphical based install
item rescue ${os} rescue mode 
item expert ${os} expert install
item preseed ${os} specify preseed url
choose --default ${type} type || goto debian

echo ${cls}
goto deb_${type}

:deb_rescue
set install_params rescue/enable=true
goto deb_text

:deb_expert
set install_params priority=low
goto deb_text

:deb_preseed
echo -n Specify preseed URL for ${os} ${version}: && read preseedurl
set install_params auto=true priority=critical preseed/url=${preseedurl}
goto deb_text

:deb_text
set dir ${dir}${menu}-installer/${arch_a}
goto deb_boot

:deb_graphical
set dir ${dir}gtk/${menu}-installer/${arch_a}
set install_params vga=788
goto deb_boot

:deb_boot
imgfree
echo Boot parameters: ${install_params} -- quiet ${params}
kernel http://${mirror}/${dir}/linux ${install_params} ${netcfg} ${mirrorcfg} -- quiet ${params} initrd=initrd.gz
initrd http://${mirror}/${dir}/initrd.gz
boot

:debian_exit
clear menu
chain linux.ipxe
